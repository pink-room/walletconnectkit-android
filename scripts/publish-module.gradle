apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'WalletConnect Kit is the Swiss Army toolkit for WalletConnect!'
                    url = 'https://github.com/pink-room/walletconnectkit-android'
                    licenses {
                        license {
                            name = 'Apache-2.0 License'
                            url = 'https://github.com/pink-room/walletconnectkit-android/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'bffcorreia'
                            name = 'Bruno Correia'
                            email = 'bruno@pinkroom.dev'
                        }
                        developer {
                            id = 'jmoreirawk'
                            name = 'Jo√£o Moreira'
                            email = 'moreira@pinkroom.dev'
                        }
                        developer {
                            id = 'rodrigosndsilva'
                            name = 'Rodrigo Silva'
                            email = 'rodrigo@pinkroom.dev'
                        }
                    }

                    scm {
                        connection = 'scm:git:github.com/pink-room/walletconnectkit-android.git'
                        developerConnection = 'scm:git:ssh://github.com/pink-room/walletconnectkit-android.git'
                        url = 'https://github.com/pink-room/walletconnectkit-android/tree/main'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}